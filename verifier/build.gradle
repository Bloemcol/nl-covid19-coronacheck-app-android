plugins {
    id "com.android.application"
    id "kotlin-android"
    id "kotlin-kapt"
    id "koin"
    id 'kotlin-parcelize'
    id "androidx.navigation.safeargs.kotlin"
}

def appVersionCode = System.getenv("GITHUB_RUN_NUMBER") != null ? System.getenv("GITHUB_RUN_NUMBER").toInteger() : 1
version = "1.0"
archivesBaseName = "verifier-v${version}-${appVersionCode}"

android {
    buildFeatures {
        viewBinding true
    }

    defaultConfig {
        applicationId "nl.rijksoverheid.ctr.verifier"
        versionCode appVersionCode
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    signingConfigs {
        release {
            storeFile file('../keystore.jks')
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEYSTORE_KEY_ALIAS")
            keyPassword System.getenv("KEYSTORE_KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "environment"
    productFlavors {
        tst {
            dimension "environment"
            versionNameSuffix "-test"
            applicationIdSuffix ".test"
            manifestPlaceholders = [appLabel: "@string/app_name_test"]
            buildConfigField "String", "URL_SUPPORT", "\"https://www.coronacheck.nl/nl/faq-scanner-in-app\""
            buildConfigField "String", "URL_ABOUT_THIS_APP", "\"https://www.coronacheck.nl/nl/over-de-scanner-app-in-app\""
            buildConfigField "String", "URL_PRIVACY_STATEMENT", "\"https://www.coronacheck.nl/nl/privacy-in-app\""
            apply plugin: 'com.google.gms.google-services'
            apply plugin: 'com.google.firebase.crashlytics'
        }
        acc {
            dimension "environment"
            versionNameSuffix "-acc"
            applicationIdSuffix ".acc"
            manifestPlaceholders = [appLabel: "@string/app_name_acc"]
            buildConfigField "String", "URL_SUPPORT", "\"https://www.coronacheck.nl/nl/faq-scanner-in-app\""
            buildConfigField "String", "URL_ABOUT_THIS_APP", "\"https://www.coronacheck.nl/nl/over-de-scanner-app-in-app\""
            buildConfigField "String", "URL_PRIVACY_STATEMENT", "\"https://www.coronacheck.nl/nl/privacy-in-app\""
            apply plugin: 'com.google.gms.google-services'
            apply plugin: 'com.google.firebase.crashlytics'
        }
        prod {
            dimension "environment"
            manifestPlaceholders = [appLabel: "@string/app_name"]
            buildConfigField "String", "URL_SUPPORT", "\"https://www.coronacheck.nl/nl/faq-scanner-in-app\""
            buildConfigField "String", "URL_ABOUT_THIS_APP", "\"https://www.coronacheck.nl/nl/over-de-scanner-app-in-app\""
            buildConfigField "String", "URL_PRIVACY_STATEMENT", "\"https://www.coronacheck.nl/nl/privacy-in-app\""
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
    }
}

dependencies {
    implementation project(":shared")
    implementation project(":appconfig")
    implementation project(":design")
    implementation project(":introduction")
    implementation project(":api")
    implementation project(":ctcl")

    implementation "com.xwray:groupie:${groupie_version}"
    implementation "com.xwray:groupie-viewbinding:${groupie_version}"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"
    implementation "org.koin:koin-android:$koin_version"
    implementation "org.koin:koin-androidx-viewmodel:$koin_version"
    implementation "org.koin:koin-androidx-fragment:$koin_version"
    implementation "androidx.preference:preference-ktx:$preference_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"
    implementation "com.jakewharton.timber:timber:${timber_version}"

    testImplementation "junit:junit:$junit_version"
    testImplementation "io.mockk:mockk:$mock_version"

    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:$desugar_jdk_version"

    tstImplementation "com.google.firebase:firebase-crashlytics-ktx:$crashlytics_version"
    accImplementation "com.google.firebase:firebase-crashlytics-ktx:$crashlytics_version"
}

